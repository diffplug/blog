plugins {
	id "com.diffplug.gradle.spotless" version "1.3.2"
}

// Travis always calls assemble, even though we don't want it to.  Not sure why.  Easy to fix though.
task assemble;
// This is what Travis calls to check that everything is fine.
task check;

////////////
// FORMAT //
////////////
String FOLLOW_LINK = '''
[![Watch](https://img.shields.io/github/watchers/diffplug/blog.svg?style=social&label=Watch)](https://github.com/diffplug/blog/subscription)
[![Star](https://img.shields.io/github/stars/diffplug/blog.svg?style=social&label=Star)](https://github.com/diffplug/blog/stargazers)
[![Follow @DiffPlug](https://img.shields.io/twitter/follow/DiffPlug.svg?style=social&label=Follow)](https://twitter.com/DiffPlug)
[![Follow @NedTwigg](https://img.shields.io/twitter/follow/NedTwigg.svg?style=social&label=Follow)](https://twitter.com/NedTwigg)
[Blog](http://discuss.diffplug.com/c/blog), echoed on [Medium](https://medium.com/diffplug)
[Facebook](https://www.facebook.com/DiffPlug)

'''
// apply formatting in the root project so that we get formatting for our README, project files, etc
spotless {
	java {
		target '**/*.java'
		licenseHeaderFile	'spotless.license.javaheader'	// License header file
		importOrderFile		'spotless.importorder'			// An import ordering file, exported from Eclipse
		eclipseFormatFile	'spotless.eclipseformat.xml'	// XML file dumped out by the Eclipse formatter
		// Eclipse formatter puts excess whitespace after lambda blocks
		//    funcThatTakesLambdas(x -> {} , y -> {} )	// what Eclipse does
		//    funcThatTakesLambdas(x -> {}, y -> {})	// what I wish Eclipse did
		custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }
	}
	format 'misc', {
		target '**/.gitignore', '**/*.gradle', '**/*.md', '**/*.sh'
		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
	freshmark {
		properties([follow: FOLLOW_LINK])
	}
}
check.dependsOn('spotlessCheck')
